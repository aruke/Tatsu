apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'koin'
apply plugin: 'com.github.triplet.play'

apply from: 'signing.gradle'
apply from: 'version.gradle'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "org.rionlabs.tatsu"
        minSdkVersion 21
        targetSdkVersion 30
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Generated from gradle.properties file (ignored)
        buildConfigField "String", "SAFETY_NET_SITE_KEY", SAFETY_NET_SITE_KEY
        buildConfigField "String", "FEEDBACK_POST_URL", FEEDBACK_POST_URL
    }

    dataBinding.enabled = true

    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            debuggable = false
            jniDebuggable = false
            renderscriptDebuggable = false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        rc {
            initWith release
            matchingFallbacks = ['release']

            signingConfig signingConfigs.rc
            applicationIdSuffix = '.rc'

            firebaseAppDistribution {
                releaseNotesFile 'app/release-notes-rc'
                groups 'android-rc-testers'
            }
        }
        debug {
            applicationIdSuffix = '.debug'

            debuggable true
            minifyEnabled false
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    androidExtensions {
        features = ["parcelize"]
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.3.8"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycleVersion"
    implementation "com.github.hadilq.liveevent:liveevent:1.2.0"

    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    implementation 'com.google.android.material:material:1.1.0'

    implementation 'com.google.firebase:firebase-analytics:19.0.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.1.0'

    implementation "android.arch.navigation:navigation-fragment-ktx:1.0.0"
    implementation "android.arch.navigation:navigation-ui-ktx:$navVersion"

    implementation "androidx.preference:preference:1.1.1"

    implementation "org.koin:koin-androidx-scope:$koinVersion"
    implementation "org.koin:koin-androidx-viewmodel:$koinVersion"

    implementation "android.arch.work:work-runtime:$workVersion"

    implementation "com.jakewharton.timber:timber:$timberVersion"

    implementation 'com.github.AppIntro:AppIntro:6.0.0'

    implementation 'com.airbnb.android:lottie:3.4.1'

    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    implementation 'com.google.android.gms:play-services-safetynet:17.0.0'

    testImplementation 'junit:junit:4.13'
    testImplementation "androidx.arch.core:core-testing:$archTesingVersion"

    testImplementation "androidx.room:room-testing:$roomVersion"

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    androidTestImplementation "android.arch.work:work-testing:$workVersion"
}

android.playConfigs {
    playConfigs {
        register("rc") { enabled = false }
    }
}

play {
    serviceAccountCredentials = file("service-account.json")
    resolutionStrategy = "auto"
    outputProcessor {
        updateVersionCode("$versionCode")
    }
}

android.defaultConfig.javaCompileOptions {
    annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schema".toString()]
    }
}